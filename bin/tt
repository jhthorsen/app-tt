#!/usr/bin/env perl
BEGIN {
  if (@ARGV and @ARGV[0] =~ /^\w/) {
    @ARGV = grep { (/^-{1,2}h\w{0,3}$/ ? ($ENV{APP_TT_HELP} = $ARGV[0], 0) : (1, 1))[1] } @ARGV;
  }
}
use Applify;
use Cwd 'abs_path';
use File::Basename;
use File::Find;
use File::HomeDir;
use File::Path 'make_path';
use File::Spec;
use JSON::XS;
use Term::ANSIColor;
use Time::Piece;
use constant DEBUG => $ENV{APP_TT_DEBUG} || 0;

sub _say { printf shift . "\n", @_ }

option str => description => 'Description for an event',            alias => 'd';
option str => tag         => 'Tags for an event',                   alias => 't', n_of => '@';
option str => project     => 'Project name. Normally autodetected', alias => 'p';

documentation __FILE__;
version '0.01';

$SIG{__DIE__} = sub { Carp::confess($_[0]) }
  if DEBUG;

sub action_analyze {
  my $self      = shift;
  my $recursive = ref $_[0] ? shift : undef;
  my $since     = shift || 'last month';
  my $dir       = shift || File::Spec->curdir;
  my $user      = qx(git config user.name) || getpwuid $<;
  my $log       = $recursive || {};

  if (-d File::Spec->catdir($dir, '.git')) {
    my $project = basename(abs_path $dir);
    local $ENV{GIT_DIR} = File::Spec->catdir($dir, '.git');
    open my $GIT, '-|', qw( git reflog --all ), "--author=$user", "--pretty=format:%ci %s", "--since=$since" or die $!;
    while (<$GIT>) {
      my ($ts, $description) = /^(\S+.\S+)\s(.*)/ or die "Invalid input: $_";
      my $tag = $description =~ /^\W*([A-Z]{2,}-[0-9]+)/ ? $1 : $description =~ /(\#\d+)/ ? $1 : $project;
      $ts = $self->_from_iso_8601($ts);
      next if $ts->hour < 5;    # should not work in the middle of the night
      $description =~ s!^\s*\+[\d:]+\s+!!;    # $ts hopefully in localtime
      my $event = $log->{$ts->ymd} ||= {stop => $ts};
      $event->{start}       = $ts if $ts <= $event->{stop};
      $event->{description} = $description;
      $event->{project}     = $project;
      $event->{tags}{$tag}  = 1;
    }
    return if $recursive;
  }
  else {
    opendir(my $DH, $dir) or die "$dir: $!";
    while (my $i = readdir $DH) {
      $self->action_analyze($log, $since, File::Spec->catdir($dir, $i)) if -d File::Spec->catdir($dir, $i, '.git');
    }
  }

  for my $event (sort { $a->{start} <=> $b->{start} } values %$log) {
    my $tags = join ',', keys %{$event->{tags}};
    _say "%s\t%s\t%s\t%s\t%s", $event->{start}->datetime, $event->{stop}->datetime, $event->{project},
      $event->{description}, $tags;
  }
}

sub action_edit {
  my $self = shift;
  my $doit = grep {/^--no-dry-run$/} @_;
  my $dir  = readlink($self->root) || $self->root;
  my $code = '';

  $code .= $_ while <STDIN>;
  $code = "sub {$code}" unless $code =~ /^\s*sub\b/s;
  $code = eval "use 5.10.1;$code" or die "Could not compile code from STDIN: $@\n";

  find {
    no_chdir => 0,
    wanted   => sub {
      /^(\d+)-(\d+)_(.*)\.trc$/ or return;
      my $f = abs_path $_;
      my $e = decode_json(_slurp($f));
      local %_ = (date => $1, doit => $doit, file => $f, hms => $2, project => $3);
      $e->{tags} ||= [];
      $code->($e) or return;
      my $trc_file = abs_path($self->_trc_path($e->{project}, $self->_from_iso_8601($e->{start})));
      return unless $doit;
      _spurt(encode_json($e) => $trc_file);
      unlink $f or die "rm $f: $!" if $f ne $trc_file;
    }
  }, $dir;
}

sub action_help {
  my $self   = shift;
  my $for    = shift || 'all';
  my $method = "_help_for_$for";

  if ($for eq 'all') {
    $self->_script->print_help;
    for (qw( log status start stop )) {
      $method = "_help_for_$_";
      print "\n---\nHelp for $_:\n";
      print $self->$method;
    }
  }
  else {
    return $self->_script->print_help unless $self->can($method);
    print $self->$method(@_);
    return 0;
  }
}

sub action_log {
  my $self       = shift;
  my $seconds    = 0;
  my $n_events   = 0;
  my $tags       = join ',', @{$self->tag};
  my @project_re = map {qr{^$_\b}} split /,/, $self->project || '.+';
  my ($interval, $offset, $pl, $path, $when, $fill) = ('', 0, 0);
  my @log;

  for (@_) {
    $offset   ||= $_ if /^(-\d+)$/;
    $interval ||= $_ if /^(?:month|year)/;
    $fill     ||= 1  if /^--fill/;
  }

  if ($interval eq 'year') {
    $when = $self->_tp(Y => $self->now->year + $offset, m => 1, d => 1);
    $path = File::Spec->catdir($self->root, $when->year);
  }
  else {
    $when = $self->_tp(m => $self->now->mon + $offset, d => 1);
    $path = File::Spec->catdir($self->root, $when->year, sprintf '%02s', $when->mon);
  }

  -d $path and find {
    no_chdir => 0,
    wanted   => sub {
      my ($date, $hms, $project) = /^(\d+)-(\d+)_(.*)\.trc$/ or return;
      my $event = decode_json(_slurp($_));
      $event->{tags} ||= [];
      return if @project_re and !grep { $event->{project} =~ $_ } @project_re;
      return if $tags and !grep { $tags =~ /\b$_\b/ } @{$event->{tags}};
      return unless $event->{seconds};
      $event->{start} = $self->_from_iso_8601($event->{start});
      push @log, $self->_fill_log_days(@log ? $log[-1]->{start} : $when, $event->{start}) if $fill;
      pop @log if @log and !$log[-1]{project} and $log[-1]{start}->mday == $event->{start}->mday;
      push @log, $event;
      $pl = length $event->{project} if $pl < length $event->{project};
      $n_events++;
      $seconds += $event->{seconds};
    }
  }, $path;

  for my $event (@log) {
    my $start = $event->{start};
    print color('red') if $start->wday == 1 or $start->wday == 7;
    printf(
      "%3s %2s %02s:%02s  %5s  %-${pl}s  %s%s\n",
      $start->month, $start->mday, $start->hour, $start->minute,
      $self->_hms_duration($event->{seconds}, 'hm'),
      $event->{project} || '---',
      join(',', @{$event->{tags}}),
      color('reset')
    );
  }

  warn sprintf "\nTotal for %s events since %s: %s\n", $n_events, join(' ', $when->month, $when->year),
    $self->_hms_duration($seconds, 'hms');
}

sub action_register {
  my ($self, $start, $stop, $project, $description, $tags) = @_;
  my ($duration, $trc_file);

  if (@_ == 1 and !-t STDIN) {
    while (<STDIN>) {
      next if /^\s*#/;
      chomp;
      my @args = split /\t/;
      $self->action_register(@args) if $args[0] and $args[1] and $args[2];
    }
    return 0;
  }

  die $self->_help_for_register unless $start and $stop and $project;

  $start    = $self->_from_iso_8601($start);
  $stop     = $self->_from_iso_8601($stop);
  $duration = $stop - $start;
  $trc_file = $self->_trc_path($project, $start);

  if (-e $trc_file) {
    warn "Already registered: $trc_file\n";
    return 1;
  }
  if ($duration < 300) {
    warn "Skipping $project - $start - $stop. Too short duration ($duration)\n";
    return 1;
  }

  my %event = (
    __CLASS__   => 'App::TimeTracker::Data::Task',
    project     => $project || $self->project,
    start       => $start->datetime,
    stop        => $stop->datetime,
    user        => scalar(getpwuid $<),
    tags        => [@{$self->tag}],
    description => $description || $self->description,
    seconds     => $duration->seconds,
    duration    => $self->_hms_duration($duration),
  );

  push @{$event{tags}}, split /,/, $tags || '';
  make_path(dirname($trc_file));
  _spurt(encode_json(\%event) => $trc_file);
  _say 'Registered "%s" at %s with duration %s', $event{project}, $event{start}, $self->_hms_duration($duration, 'hms');
}

sub action_start {
  my ($self, @args) = @_;
  my $event = {};
  my $trc_file;

  $self->project($args[0]) if $args[0] and $args[0] =~ /^[A-Za-z0-9-]+$/;
  $self->project(basename(Cwd::getcwd)) if -d '.git' and !$self->project;
  $trc_file = $self->_trc_path($self->project, $self->now);

  warn "[APP_TT] start $trc_file\n" if DEBUG;

  if (!$self->project) {
    warn "Cannot 'start' with unknown project name. Are you sure you are inside a git project?\n";
    return 1;    # Operation not permitted
  }

  $self->_stop_previous({start => 1});
  $self->_add_event_info($event);
  make_path(dirname($trc_file));
  _spurt(encode_json($event) => $trc_file);
  _spurt($trc_file => File::Spec->catfile($self->root, 'previous'));
  _say 'Started working on project "%s" at %s.', $event->{project}, $self->now->hms(':');
}

sub action_stop {
  my ($self, @args) = @_;
  return $self->_stop_previous;
}

sub action_status {
  my $self = shift;
  my ($trc_file, $event) = $self->_get_previous_event;

  warn "[APP_TT] status $trc_file\n" if DEBUG;

  if (!$event->{start}) {
    _say 'No event is being tracked.';
    return 3;    # No such process
  }
  elsif ($event->{stop}) {
    _say 'Stopped working on "%s" at %s after %s', $event->{project}, $event->{stop}, $event->{duration};
    return 3;    # No such process
  }
  else {
    my $duration = $self->now - $self->_from_iso_8601($event->{start}) + $self->tzoffset;
    _say 'Been working on "%s", for %s', $event->{project}, $self->_hms_duration($duration, 'hms');
    return 0;
  }
}

sub now { shift->{now} ||= localtime }

sub root {
  shift->{root} ||= $ENV{TIMETRACKER_HOME} || do {
    my $home = File::HomeDir->my_home || File::Spec->curdir;
    File::Spec->catdir($home, '.TimeTracker');
  };
}

sub tzoffset {
  my $self = shift;
  $self->{tzoffset} ||= $self->now->tzoffset;
}

sub _add_event_info {
  my ($self, $event) = @_;
  my $tags = $self->tag || [];

  $event->{__CLASS__} ||= 'App::TimeTracker::Data::Task';
  $event->{project}   ||= $self->project;
  $event->{seconds}   ||= undef;
  $event->{start}     ||= $self->now->datetime;
  $event->{user}      ||= scalar(getpwuid $<);
  $event->{tags}      ||= [];

  $event->{description} = $self->description if $self->description;

  for my $t (ref $tags ? @$tags : $tags) {
    push @{$event->{tags}}, $t;
  }
}

sub _fill_log_days {
  my ($self, $last, $now) = @_;
  my $interval = int(($now - $last)->days);

  map {
    my $t = $last + $_ * 86400;
    +{seconds => 0, start => $t, tags => [$t->day]}
  } 1 .. $interval;
}

sub _from_iso_8601 {
  my ($self, $str) = @_;
  $str =~ s/(\d)\s(\d)/${1}T${2}/;
  $str =~ s/\.\d+$//;
  Time::Piece->strptime($str, '%Y-%m-%dT%H:%M:%S');
}

sub _get_previous_event {
  my $self = shift;
  my $trc_file = File::Spec->catfile($self->root, 'previous');

  warn "[APP_TT] _get_previous_event $trc_file\n" if DEBUG;

  return $trc_file, {} unless -r $trc_file;
  $trc_file = _slurp($trc_file);    # $ROOT/previous contains path to last .trc file
  $trc_file =~ s!\s*$!!;
  return $trc_file, {} unless -r $trc_file;
  return $trc_file, decode_json(_slurp($trc_file));    # slurp $ROOT/2015/08/20150827-085643_app_tt.trc
}

sub _help_for_analyze {
  <<'HERE';
This action can analyze all the git repos in a directory and print what time
you started/stopped working on a set of repos grouped per day. The output
is suitable for "tt register".

$ cd ..                  # one step up from the current git-project
$ tt analyze             # default is last month
$ tt analyze "last week" # analyze git reflog from last week
$ tt analyze 2015-07-01  # analyze git reflog since a given date
HERE
}

sub _help_for_edit {
  <<'HERE';

This command can be used to rewrite all the log entries. See source
code before running this action. (Internals might change)

DISCLAIMER! Backup your files before running this action!

$ cat rewrite.pl | tt edit
# verify output, and then run:
$ cat rewrite.pl | tt edit --no-dry-run
HERE
}

sub _help_for_log {
  <<'HERE';

This command will report how much time you have spent on various
events.

$ tt log         # this month
$ tt log -2      # two months ago
$ tt log year    # log for year
$ tt log year -1 # last year
$ tt log -p foo  # Filter by project name
HERE
}

sub _help_for_register {
  <<'HERE';

This command is used to import data from other sources. "project-name" default to
"-p" or current git project, "some description" default to "-d" and tags can be
specified by -t foo -t bar

$ tt register 2010-01-21T18:50:00  2010-01-21T19:20:00 "project-name" "some description" "foo,bar"
$ echo "2010-01-21T18:50:00\t2010-01-21T19:20:00\tproject-name\tsome description\tfoo,bar" | tt register
HERE
}

sub _help_for_start {
  <<'HERE';

This command will start tracking a new event. It will also stop the
current event if any event is in process. This action takes the
"-p" and "-t" switches. "-p" (project) is not required if you start
from a git repository.

# Specify a tag and custom project name
$ tt start -t ISSUE-999 some-project-name

# Started working at 08:00 instead of now
$ tt start 08:00
HERE
}

sub _help_for_status {
  <<'HERE';
Get the current status: Are you working on something or not?

$ tt status
HERE
}

sub _help_for_stop {
  <<'HERE';

This command will stop tracking the current event.

# Add more tags to the current event
$ tt stop -t meetings -t not_fun

# Stop working at 16:00 instead of now
$ tt stop 16:00
HERE
}

sub _hms_duration {
  my ($self, $duration, $sep) = @_;
  my $seconds = int(ref $duration ? $duration->seconds : $duration);
  my ($hours, $minutes);

  $hours = int($seconds / 3600);
  $seconds -= $hours * 3600;
  $minutes = int($seconds / 60);
  $seconds -= $minutes * 60;

  return sprintf '%s:%02s:%02s', $hours, $minutes, $seconds if !$sep;
  return sprintf '%2s:%02s', $hours, $minutes if $sep eq 'hm';
  return sprintf '%sh %sm %ss', $hours, $minutes, $seconds;
}

# From Mojo::Util
sub _slurp {
  my $path = shift;
  die qq{Can't open file "$path": $!} unless open my $file, '<', $path;
  my $content = '';
  while ($file->sysread(my $buffer, 131072, 0)) { $content .= $buffer }
  return $content;
}

# From Mojo::Util
sub _spurt {
  my ($content, $path) = @_;
  die qq{Can't open file "$path": $!} unless open my $file, '>', $path;
  die qq{Can't write to file "$path": $!} unless defined $file->syswrite($content);
  return $content;
}

sub _stop_previous {
  my ($self,     $args)  = @_;
  my ($trc_file, $event) = $self->_get_previous_event;

  if (!$event->{start} or $event->{stop}) {
    return 0 if $args->{start};
    warn "No previous event to stop.\n";
    return 3;    # No such process
  }

  my $duration = $self->now - $self->_from_iso_8601($event->{start}) + $self->tzoffset;

  # Probably some invalid timestamp was given as input
  if ($duration->seconds < 0) {
    die "Cannot log event shorter than a second! Need to manually fix $trc_file";
  }

  $event->{duration} = $self->_hms_duration($duration);
  $event->{seconds}  = $duration->seconds;
  $event->{stop}     = $self->now->datetime;

  if ($event->{seconds} <= 300) {
    _say 'Dropping log event for "%s" since worked less than five minutes.', $event->{project};
    unlink $trc_file or die "rm $trc_file: $!";
  }
  else {
    $self->_add_event_info($event);
    _spurt(encode_json($event) => $trc_file);
    _say 'Stopped working on "%s" after %s', $event->{project}, $self->_hms_duration($duration, 'hms');
  }
}

sub _tp {
  my ($self, %t) = @_;

  $t{Y} ||= $self->now->year;
  $t{m} ||= $self->now->mon;
  $t{d} ||= $self->now->mday;
  $t{H} ||= 0;
  $t{M} ||= 0;
  $t{S} ||= 0;

  if ($t{m} < 0) {
    $t{m} = 12 - $t{m};
    $t{Y}--;
  }

  Time::Piece->strptime(sprintf('%s-%s-%sT%s:%s:%s+0000', @t{qw( Y m d H M S )}), '%Y-%m-%dT%H:%M:%S%z');
}

sub _trc_path {
  my ($self, $project, $t) = @_;
  my $month = sprintf '%02s', $t->mon;
  my $file;

  $project =~ s!\W!_!g;
  $file = sprintf '%s-%s_%s.trc', $t->ymd(''), $t->hms(''), $project;

  return File::Spec->catfile($self->root, $t->year, $month, $file);
}

app {
  my $self = shift;
  my $action = sprintf 'action_%s', shift || 'status';

  if ($ENV{APP_TT_HELP}) {
    return $self->action_help($ENV{APP_TT_HELP});
  }
  if (!$self->description) {
    my ($description) = grep {/^\w\S*\s/} @_;
    $self->description($description) if $description;
  }
  if (my ($hm) = grep {/^\d+:\d+$/} @_) {
    $hm =~ /^(\d+):(\d+)$/;
    $self->{now} = $self->_tp(H => $1, M => $2);
  }

  return $self->action_help('app') unless $self->can($action);
  return $self->$action(@_);
};

=head1 NAME

tt - Time tracking utility

=head1 SYNOPSIS

Basic usage:

  # Start to track time
  $ cd $HOME/git/my-project
  $ tt start
  # Work, work, work, cd ..., do other stuff
  $ tt stop

A bit more complex:

  # Start to work on an event and add a tag
  $ tt start -t ISSUE-999 -p some-project-at-work
  # Add another tag to the same event and add a --comment
  $ tt stop -t GITHUB-1005 "Today I was mostly in meetings"

Available actions: (each action takes "-h" for more details)

  $ tt {analyze,log,start,stop,status,register}
  $ tt start -h

=head1 DISCLAIMER

This code is not tested.

=head1 AUTHOR

Jan Henning Thorsen - jhthorsen@cpan.org

=cut

__END__
cat <<HERE | sqlite3 ~/Downloads/hamster.db -separator $'\t' | bin/tt register
SELECT f.start_time,f.end_time,a.name,f.description,group_concat(t.name)
FROM facts f
LEFT JOIN activities a ON a.id              = f.activity_id
LEFT JOIN fact_tags    ON fact_tags.fact_id = f.id
LEFT JOIN tags t       ON t.id              = fact_tags.tag_id
GROUP BY fact_tags.fact_id;
HERE

cat <<HERE | sqlite3 ~/Downloads/hamster.db -separator $'\t' | bin/tt register
SELECT f.start_time,f.end_time,a.name,f.description
FROM facts f LEFT JOIN activities a ON a.id = f.activity_id;
HERE
